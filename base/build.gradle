apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "$rootDir/config/config.gradle"
apply from: "$rootDir/depedencycheck.gradle"

android {
    ndkVersion "23.0.7599858"
    compileOptions {
        sourceCompatibility rootProject.java
        targetCompatibility rootProject.java
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    compileSdkVersion rootProject.appCompiledSdk
    defaultConfig {
        minSdkVersion rootProject.appMinSdk
        targetSdkVersion rootProject.appTargetSdk
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        fdroid {
            main.jniLibs.srcDirs = ['/frdroid/src/libs', 'libs']
        }
        google {
            main.jniLibs.srcDirs = ['/google/src/libs', 'libs']
        }
    }
    packagingOptions {
        jniLibs {
            excludes += ['lib/arm64-v8a/libwg-quick.so', 'lib/armeabi-v7a/libwg-quick.so', 'lib/x86/libwg-quick.so', 'lib/x86_64/libwg-quick.so'
            ]
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation', 'NonConstantResourceId', 'VectorRaster', 'ContentDescription'
    }
    namespace 'com.windscribe.vpn'
    //Force gradle to pick latest version of guava and junit.
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.google.guava') {
                def version = details.requested.version
                def versionData = version.split('-')
                if (versionData.contains('jre')) {
                    details.useVersion "30.1.1-jre"
                }
                if (versionData.contains('android')) {
                    details.useVersion "30.1.1-android"
                }
            }
            if (details.requested.group == 'junit') {
                details.useVersion "4.13.2"
            }
        }
    }
    flavorDimensions "dim"
    productFlavors {
        fdroid {
            dimension = "dim"
        }
        google {
            dimension = "dim"
        }
    }
}

dependencies {
    //Android
    api "androidx.appcompat:appcompat:$appCompat"
    api "com.google.android.material:material:$material"
    api 'androidx.vectordrawable:vectordrawable:1.1.0'
    api 'androidx.transition:transition:1.4.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.multidex:multidex:2.0.1'
    api "androidx.security:security-crypto:1.1.0-alpha05"
    api "androidx.lifecycle:lifecycle-extensions:2.2.0"
    api "androidx.lifecycle:lifecycle-common-java8:$archLifecycle"
    runtimeOnly "androidx.lifecycle:lifecycle-runtime-ktx:$archLifecycle"
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_std_lib"
    implementation "androidx.core:core-ktx:$ktx_core"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.6.4'
    //VPN
    implementation project(path: ':openvpn')
    implementation project(path: ':strongswan')
    api 'com.wireguard.android:tunnel:1.0.20230427'
    implementation 'com.github.Windscribe:wstunnel:1.0.3'
    //Rxjava
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxjava:2.2.13'
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    api "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    //Okhttp
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    api 'com.squareup.okhttp3:mockwebserver:4.9.3'
    //Logging
    api 'org.slf4j:slf4j-api:1.7.36'
    api 'com.github.tony19:logback-android:2.0.0'
    //Room
    api "androidx.room:room-rxjava2:$room"
    api "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"
    implementation "androidx.room:room-testing:$room"
    // Work manager
    api "androidx.work:work-runtime:$work_manager"
    api "androidx.work:work-rxjava2:$work_manager"
    implementation "androidx.work:work-runtime-ktx:$work_manager"
    androidTestImplementation "androidx.work:work-testing:$work_manager"
    androidTestImplementation "androidx.room:room-testing:$work_manager"
    // Dagger
    kapt "com.google.dagger:dagger-compiler:$dagger"
    api "com.google.dagger:dagger:$dagger"
    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesCore"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesCore"
    // Supporting libraries
    api 'org.javatuples:javatuples:1.2'
    api 'com.github.seancfoley:ipaddress:5.3.1'
    api "com.github.bumptech.glide:glide:$glide"
    kapt "com.github.bumptech.glide:compiler:$glide"
    api 'commons-io:commons-io:2.11.0'
    api 'com.thoughtbot:expandablerecyclerview:1.3'
    api 'com.jakewharton:butterknife:10.2.0'
    kapt 'com.jakewharton:butterknife-compiler:10.2.3'
    api 'com.google.guava:guava:30.1.1-android'
    api 'net.grandcentrix.tray:tray:0.12.0'
    implementation 'org.isomorphism:token-bucket:1.6'
    // Test
    testImplementation "junit:junit:$JUnit"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    // ECH Conscrypt
    implementation 'info.guardianproject.conscrypt:conscrypt-android:2.6.alpha1638179154.job1828169525'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'commons-net:commons-net:3.6'
    // Google only dependencies
    googleApi "com.google.firebase:firebase-messaging:$fireBase"
    googleApi files("$projectDir/src/google/libs/in-app-purchasing-2.0.76.jar")
    googleApi 'com.android.billingclient:billing:5.0.0'
}
