/*
 * Copyright (c) 2021 Windscribe Limited.
 */
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
apply from: "$rootDir/config/config.gradle"
apply from: "$rootDir/depedencycheck.gradle"

android {
    namespace 'com.windscribe.tv'
    bundle {
        language {
            enableSplit = false
        }
    }

    compileSdkVersion rootProject.appCompiledSdk
    defaultConfig {
        applicationId rootProject.AppId
        minSdkVersion rootProject.appMinSdk
        targetSdkVersion rootProject.appTargetSdk
        versionName System.getenv().getOrDefault("VERSION_NAME", "3.74")
        versionCode 1210
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders["networkSecurityConfig"] = "@xml/network_security_config"
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders["networkSecurityConfig"] = "@xml/test_network_security_config"
        }
    }
    flavorDimensions "dim"
    productFlavors {
        fdroid {
            dimension = "dim"
        }
        google {
            dimension = "dim"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'NonConstantResourceId', 'ContentDescription', 'VectorRaster'
    }
}

dependencies {
    //Android
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation "com.google.android.material:material:$material"
    implementation "androidx.leanback:leanback:$leanBack"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin"
    implementation "androidx.core:core-ktx:$ktx_core"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugarJdk"
    //Core
    implementation project(path: ':base')
    //Annotation
    annotationProcessor "com.github.bumptech.glide:compiler:$glide"
    annotationProcessor "androidx.room:room-compiler:$room"
    //Kapt
    kapt "androidx.room:room-compiler:$room"
    kapt "com.jakewharton:butterknife-compiler:$butterknife"
    kapt "com.google.dagger:dagger-compiler:$dagger"
}
